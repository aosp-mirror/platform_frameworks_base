/*
 * Copyright (C) 2015 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */


apply plugin: 'maven'
apply plugin: 'com.android.library'
apply plugin: 'com.android.databinding'

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile "com.android.databinding:baseLibrary:${config.snapshotVersion}"
    provided "com.android.databinding:annotationprocessor:${config.snapshotVersion}"
    compile 'com.android.support:support-v4:+'
    compile 'com.android.support:cardview-v7:+'
    compile 'com.android.support:appcompat-v7:+'
}

configurations {
    jarArchives
}


//create jar tasks
android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
        return; // Skip debug builds.
    }
    // @Jar version is needed to run compiler tests
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    def packageName = "com.android.databinding.library.baseAdapters"
    def appPkgAsClass = packageName.replace('.', '/')
    task.exclude("com/android/databinding/layouts/*.*")
    task.exclude("$appPkgAsClass/generated/*")
    task.exclude("$appPkgAsClass/BR.*")
    artifacts.add('jarArchives', task);
}

uploadArchives {
}

uploadJarArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${config.mavenRepoDir}")
            pom.artifactId = "adapters"
            pom.whenConfigured {
                println("configured pom, $it")
                it.dependencies.find {dep -> dep.groupId == 'com.android.support' && dep.artifactId == 'support-v4' }.optional = true
                it.dependencies.find {dep -> dep.groupId == 'com.android.support' && dep.artifactId == 'cardview-v7' }.optional = true
                it.dependencies.find {dep -> dep.groupId == 'com.android.support' && dep.artifactId == 'appcompat-v7' }.optional = true
            }
        }
    }
}

uploadArchives.dependsOn uploadJarArchives
